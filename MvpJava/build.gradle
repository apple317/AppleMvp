apply plugin: 'com.android.library'
android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

}

dependencies {
    api 'com.trello.rxlifecycle3:rxlifecycle:3.0.0'
    api 'androidx.multidex:multidex:2.0.0'
    api 'com.trello.rxlifecycle3:rxlifecycle-components:3.0.0'
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//以下是需要针对项目需要修改的配置内容
//发布者的组织名称
group = "com.applehsp.mvp"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.0"
//上面配置后上传至bintray后的编译路径是这样的： compile 'com.clwater:bintraylibrary:0.0.1'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
//读取 local.properties 文件里面的 bintray.user
def bintrayUser = properties.getProperty("bintray.user")
//读取 local.properties 文件里面的 bintray.apikey
def bintrayKey = properties.getProperty("bintray.apikey")

//项目主页
def siteUrl = 'https://github.com/apple317/AppleMvp.git'
//项目的版本控制地址
def gitUrl = 'https://github.com/apple317/AppleMvp.git'
//发布到JCenter上的项目名字，必须填写
def libName = "mvp-java"
//文档连接
def javaDocLinks = "https://github.com/clwater"
//Bintray中Repository的名字
def bintrayRepo = "com.applehsp.mvp"
//应用的描述
def bintrayDesc = "Desc"
//组织的名字, 建立组织账号后上传的时候需要这个, 个人张海时候不需要修改这部分
def bintrayUserOrg = "applehusp"

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    //以下两个为项目中带有中文注释的支持
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links javaDocLinks
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                //项目名称和描述
                name 'pom project name'
                description 'pom project escription'
                url siteUrl
                licenses {
                    license {
                        //开源协议,在bintray中创建仓库时选择的license为Apache-2.0，复制下面的就可以
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    //开发者的个人信息
                    developer {
                        id 'developer id '
                        name 'developer name '
                        email 'developer email'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    pkg {
        userOrg = bintrayUserOrg
        repo = bintrayRepo
        name = libName
        desc = bintrayDesc
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
